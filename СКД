Функция ПолучитьТЗИзОтчета(ГраницаПросрочки, Организации) Экспорт
	
	Результат = Новый ТаблицаЗначений;
	
	СхемаКомпоновкиДанных = Отчеты.ОтчетПоПросроченнойЗадолженности.ПолучитьМакет("ОсновнаяСхемаКомпоновкиДанных");
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	
	Настройки = СхемаКомпоновкиДанных.НастройкиПоУмолчанию;
	Настройки.ПараметрыДанных.УстановитьЗначениеПараметра("СрокПросрочки", ГраницаПросрочки);
	
	Если Организации <> Неопределено Тогда
		Настройки.ПараметрыДанных.УстановитьЗначениеПараметра("Организация", Организации);
	КонецЕсли;

	МакетКомпоновки   = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, Настройки,,,Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений"));
    
    //Создаем процессор компоновки
    ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
    ПроцессорКомпоновки.Инициализировать(МакетКомпоновки,,,Истина);
        
    //Выводим в таблицу значений
    ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений;
    ПроцессорВывода.УстановитьОбъект(Результат);
    Результат = ПроцессорВывода.Вывести(ПроцессорКомпоновки, Истина);
    Возврат Результат;
    
КонецФункции

Процедура СформироватьТабличныйДокументПоТаблице(ТабДок, ТаблицаДанных, Колонки = "") Экспорт
		
	Построитель = Новый ПостроительОтчета;
	Если ЗначениеЗаполнено(Колонки) Тогда
		Построитель.ИсточникДанных = Новый ОписаниеИсточникаДанных(ТаблицаДанных.Скопировать(, Колонки));
	Иначе
		Построитель.ИсточникДанных = Новый ОписаниеИсточникаДанных(ТаблицаДанных);	
	КонецЕсли;
	Построитель.Вывести(ТабДок);
	
КонецПроцедуры
