Функция КоличествоВхожденийПодстрокиВСтроку(Строка, ПодстрокаПоиска, УчетРегистраСтроки) Экспорт
  
  КоличествоВхождений = 0;
  
  Если УчетРегистраСтроки = "БезУчетаРегистраСтроки" Тогда
    пСтрока = ВРЕГ(Строка);
    пПодстрокаПоиска = ВРЕГ(ПодстрокаПоиска);  
  ИначеЕсли УчетРегистраСтроки = "СУчетомРегистраСтроки" Тогда
    пСтрока = Строка;
    пПодстрокаПоиска = ПодстрокаПоиска;
  КонецЕсли;
  
  ДлинаСтроки = СтрДлина(пСтрока);
  ДлинаПодстрокиПоиска = СтрДлина(пПодстрокаПоиска);
  НачальнаяПозиция = 1;
  
  ПозицияПодстрокиПоиска = СтрНайти(пСтрока, пПодстрокаПоиска,, НачальнаяПозиция);
  Пока ПозицияПодстрокиПоиска > 0 Цикл
    КоличествоВхождений = КоличествоВхождений + 1;
    ПозицияПодстрокиПоиска = ПозицияПодстрокиПоиска + 1;
    
    Если ПозицияПодстрокиПоиска > ДлинаСтроки Тогда
      Прервать;
    КонецЕсли;
    
    ПозицияПодстрокиПоиска = СтрНайти(пСтрока, пПодстрокаПоиска,, ПозицияПодстрокиПоиска);  
  КонецЦикла;
  
  Возврат КоличествоВхождений;
  
КонецФункции

Функция ИзСтрокиВДатуПоМаске(ДатаСтрокой, Маска) Экспорт

  ОтветФункции = Новый Структура;
  ОтветФункции.Вставить("ДатаСтрокой", ДатаСтрокой);
  ОтветФункции.Вставить("Маска", Маска);
  ОтветФункции.Вставить("Дата", Дата(1,1,1));
  
  Если НЕ ЗначениеЗаполнено(ДатаСтрокой) Тогда
    Возврат ОтветФункции;  
  КонецЕсли;
  
  Если НЕ ЗначениеЗаполнено(Маска) Тогда
    Возврат ОтветФункции;  
  КонецЕсли;
  
  Если СтрДлина(ДатаСтрокой) <> СтрДлина(Маска) Тогда
    Возврат ОтветФункции;  
  КонецЕсли;
  
  //
  КолВоВхождений = КоличествоВхожденийПодстрокиВСтроку(Маска, "гггг", "СУчетомРегистраСтроки");
  Если КолВоВхождений <> 1 Тогда
    Возврат ОтветФункции;  
  КонецЕсли;
  
  КолВоВхождений = КоличествоВхожденийПодстрокиВСтроку(Маска, "ММ", "СУчетомРегистраСтроки");
  Если КолВоВхождений <> 1 Тогда
    Возврат ОтветФункции;  
  КонецЕсли;
  
  КолВоВхождений = КоличествоВхожденийПодстрокиВСтроку(Маска, "дд", "СУчетомРегистраСтроки");
  Если КолВоВхождений <> 1 Тогда
    Возврат ОтветФункции;  
  КонецЕсли;
  
  ДатаСВременем = (СтрНайти(Маска, "ЧЧ") > 0) И (СтрНайти(Маска, "мм") > 0) И (СтрНайти(Маска, "сс") > 0);
  
  Если ДатаСВременем Тогда
    КолВоВхождений = КоличествоВхожденийПодстрокиВСтроку(Маска, "ЧЧ", "СУчетомРегистраСтроки");
    Если КолВоВхождений <> 1 Тогда
      Возврат ОтветФункции;  
    КонецЕсли;
    
    КолВоВхождений = КоличествоВхожденийПодстрокиВСтроку(Маска, "мм", "СУчетомРегистраСтроки");
    Если КолВоВхождений <> 1 Тогда
      Возврат ОтветФункции;  
    КонецЕсли;
    
    КолВоВхождений = КоличествоВхожденийПодстрокиВСтроку(Маска, "сс", "СУчетомРегистраСтроки");
    Если КолВоВхождений <> 1 Тогда
      Возврат ОтветФункции;  
    КонецЕсли;
  КонецЕсли;
  
  //
  ПозицияГод = СтрНайти(Маска, "гггг");
  Год = Сред(ДатаСтрокой, ПозицияГод, 4);
  
  ПозицияМесяц = СтрНайти(Маска, "ММ");
  Месяц = Сред(ДатаСтрокой, ПозицияМесяц, 2);
  
  ПозицияДень = СтрНайти(Маска, "дд");
  День = Сред(ДатаСтрокой, ПозицияДень, 2);
  
  Если ДатаСВременем Тогда
    ПозицияЧас = СтрНайти(Маска, "ЧЧ");
    Час = Сред(ДатаСтрокой, ПозицияЧас, 2);
    
    ПозицияМинута = СтрНайти(Маска, "мм");
    Минута = Сред(ДатаСтрокой, ПозицияМинута, 2);
    
    ПозицияСекунда = СтрНайти(Маска, "сс");
    Секунда = Сред(ДатаСтрокой, ПозицияСекунда, 2);
  Иначе
    Час = "00";
    Минута = "00";
    Секунда = "00";
  КонецЕсли;
  
  //
    ОписаниеТипаДата = Новый ОписаниеТипов("Дата",,,,, Новый КвалификаторыДаты(ЧастиДаты.ДатаВремя));
    ОтветФункции.Дата = ОписаниеТипаДата.ПривестиЗначение(Год + Месяц + День + Час + Минута + Секунда);
  
  Возврат ОтветФункции;
  
КонецФункции

// Преобразует исходную строку в число без вызова исключений.
//
// Параметры:
//   Значение - Строка - строка, которую необходимо привести к числу.
//                       Например, "10", "+10", "010", вернет 10;
//                                 "(10)", "-10",вернет -10;
//                                 "10,2", "10.2",вернет 10.2;
//                                 "000", " ", "",вернет 0;
//                                 "10текст", вернет Неопределено.
//
// Возвращаемое значение:
//   Число, Неопределено - полученное число, либо Неопределено, если строка не является числом.
//
Функция СтрокаВЧисло(Знач Значение) Экспорт
	
	Значение  = СтрЗаменить(Значение, " ", "");
	Если СтрНачинаетсяС(Значение, "(") Тогда
		Значение = СтрЗаменить(Значение, "(", "-");
		Значение = СтрЗаменить(Значение, ")", "");
	КонецЕсли;
	
	СтрокаБезНулей = СтрЗаменить(Значение, "0", "");
	Если ПустаяСтрока(СтрокаБезНулей) Или СтрокаБезНулей = "-" Тогда
		Возврат 0;
	КонецЕсли;
	
	ТипЧисло  = Новый ОписаниеТипов("Число");
	Результат = ТипЧисло.ПривестиЗначение(Значение);
	
	Возврат ?(Результат <> 0 И Не ПустаяСтрока(СтрокаБезНулей), Результат, Неопределено);
	
КонецФункции
