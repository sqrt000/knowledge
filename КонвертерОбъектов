Функция ИзРезультатаЗапросаВМассивСтруктур(РезультатЗапроса) Экспорт
  
  МассивСтрок = Новый Массив;
  
  Если РезультатЗапроса.Пустой() Тогда
    Возврат МассивСтрок;
  КонецЕсли;
  
  Выборка = РезультатЗапроса.Выбрать();
  Пока Выборка.Следующий() Цикл
    СтрокаПриемник = Новый Структура;
    
    Для Каждого Колонка Из РезультатЗапроса.Колонки Цикл    
      Если ТипЗнч(Выборка[Колонка.Имя]) = Тип("РезультатЗапроса") Тогда
        СтрокаПриемник.Вставить(Колонка.Имя, ИзРезультатаЗапросаВМассивСтруктур(Выборка[Колонка.Имя]));  
      Иначе
        СтрокаПриемник.Вставить(Колонка.Имя, Выборка[Колонка.Имя]);
      КонецЕсли;
    КонецЦикла;
    
    МассивСтрок.Добавить(СтрокаПриемник);
  КонецЦикла;
    
  Возврат МассивСтрок;
    
КонецФункции //(ИзРезультатаЗапросаВМассивСтруктур)

Функция ИзТаблицыЗначенийВМассивСтруктур(ТаблицаЗначений) Экспорт
  
  МассивСтрок = Новый Массив;
  
  Если ТаблицаЗначений.Количество() = 0 Тогда
    Возврат МассивСтрок;
  КонецЕсли;
  
  Для Каждого СтрокаТЗ Из ТаблицаЗначений Цикл
    СтрокаПриемник = Новый Структура;
    
    Для Каждого Колонка Из ТаблицаЗначений.Колонки Цикл    
      Если ТипЗнч(СтрокаТЗ[Колонка.Имя]) = Тип("ТаблицаЗначений") Тогда
        СтрокаПриемник.Вставить(Колонка.Имя, ИзТаблицыЗначенийВМассивСтруктур(СтрокаТЗ[Колонка.Имя]));  
      Иначе
        СтрокаПриемник.Вставить(Колонка.Имя, СтрокаТЗ[Колонка.Имя]);
      КонецЕсли;
    КонецЦикла;
    
    МассивСтрок.Добавить(СтрокаПриемник);
  КонецЦикла;
    
  Возврат МассивСтрок;    
  
КонецФункции //(ИзТаблицыЗначенийВМассивСтруктур)
