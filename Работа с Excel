
#Область ПрикладныеМетоды

Функция ПрочитатьПервыйЛистExcel(ИмяФайла, НомерПервойСтроки = 1, НомерПервойКолонки = 1, ВсегоСтрок = 0, ВсегоКолонок = 0, ОписаниеОшибки = "") Экспорт
	
	Отказ = Ложь;
		
	Эксель = РаботаСExcel.ПолучитьExcel(Отказ, ОписаниеОшибки);
	Если Отказ Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	КнигаЭксель = РаботаСExcel.ПолучитьКнигуExcel(Эксель, ИмяФайла, Отказ, ОписаниеОшибки);
	Если Отказ Тогда
		РаботаСExcel.ЗакрытьExcel(Эксель);
		Возврат Неопределено;	
	КонецЕсли;
	
	ЛистЭксель = РаботаСExcel.ПолучитьЛистExcel(КнигаЭксель, 1, Отказ, ОписаниеОшибки);
	Если Отказ Тогда
		ОписаниеОшибки = "Не удалось открыть лист номер " + 1 + ". ОписаниеОшибки: " + ОписаниеОшибки;
		РаботаСExcel.ЗакрытьКнигуExcel(КнигаЭксель);
		РаботаСExcel.ЗакрытьExcel(Эксель);
		Возврат Неопределено;
	КонецЕсли;
	РезультатЧтенияЛиста = РаботаСExcel.ПрочитатьЛистExcel(ЛистЭксель, НомерПервойСтроки, НомерПервойКолонки, ВсегоСтрок, ВсегоКолонок);
		
	РаботаСExcel.ЗакрытьКнигуExcel(КнигаЭксель);
	РаботаСExcel.ЗакрытьExcel(Эксель);
	
	Если РезультатЧтенияЛиста = Неопределено Тогда
		ОписаниеОшибки = "Не удалось прочитать файл";
		Возврат Неопределено;
	КонецЕсли;
	
	Если РезультатЧтенияЛиста.Количество() = 0 Тогда
		ОписаниеОшибки = "Файл пустой";
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат РезультатЧтенияЛиста;
КонецФункции

Функция ЗаписатьТаблицуЗначенийНаПервыйЛист(ИмяФайла, ТаблицаЗначений, ОписаниеОшибки = "") Экспорт
	
	Отказ = Ложь;
	ОписаниеОшибки = "";
	
	Эксель = ПолучитьExcel(Отказ, ОписаниеОшибки);
	Если Отказ Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	КнигаЭксель = ДобавитьКнигуExcel(Эксель, Отказ, ОписаниеОшибки);
	Если Отказ Тогда
		РаботаСExcel.ЗакрытьExcel(Эксель);
		Возврат Неопределено;	
	КонецЕсли;
	
	ЛистЗаказы = КнигаЭксель.Worksheets(1);
	ЗаполнитьЛистExcelИзТаблицыЗначений(ЛистЗаказы, ТаблицаЗначений);

	// Сохранение файла
	КнигаЭксель.SaveAs(ИмяФайла);
	
	РаботаСExcel.ЗакрытьКнигуExcel(КнигаЭксель);
	РаботаСExcel.ЗакрытьExcel(Эксель);
	
КонецФункции

#КонецОбласти

#Область ОбщиеПроцедуры

////ОТКРЫТЬ////
Функция ПолучитьExcel(Отказ = Ложь, ОписаниеОшибки = "") Экспорт
	
	Эксель = Неопределено;
	Попытка
		Эксель = Новый COMОбъект("Excel.Application");
		Эксель.Visible = Ложь; 
		Эксель.DisplayAlerts=Ложь;		
	Исключение
		ОписаниеОшибки = "Не удалось получить COM-объект. Описание ошибки: " + ОписаниеОшибки();
		Отказ = Истина;	
	КонецПопытки;
	
	Возврат Эксель;
КонецФункции

Функция ПолучитьКнигуExcel(Эксель, ИмяФайла, Отказ = Ложь, ОписаниеОшибки = "") Экспорт
	
	КнигаЭксель = Неопределено;
	Попытка
		КнигаЭксель = Эксель.WorkBooks.Open(ИмяФайла);
	Исключение
		ОписаниеОшибки = "Не удалось открыть файл Excel. Описание ошибки: " + ОписаниеОшибки();
		Отказ = Истина;	
	КонецПопытки;
	
	Возврат КнигаЭксель;
КонецФункции

Функция ПолучитьЛистExcel(КнигаЭксель, ИмяИлиНомерЛиста = 1, Отказ = Ложь, ОписаниеОшибки = "") Экспорт
	
	ЛистЭксель = Неопределено;
	Попытка
		ЛистЭксель = КнигаЭксель.WorkSheets(ИмяИлиНомерЛиста);
	Исключение
		ОписаниеОшибки = "Не удалось открыть лист Excel. ИмяИлиНомерЛиста: " + ИмяИлиНомерЛиста + ". Описание ошибки: " + ОписаниеОшибки();
		Отказ = Истина;;	
	КонецПопытки;
	
	Возврат ЛистЭксель;
КонецФункции

////ДОБАВИТЬ////
Функция ДобавитьКнигуExcel(Эксель, Отказ, ОписаниеОшибки) Экспорт
	
	КнигаЭксель = Неопределено;
	Попытка
		КнигаЭксель = Эксель.WorkBooks.Add();
	Исключение
		ОписаниеОшибки = "Не удалось добавить книгу. Описание ошибки: " + ОписаниеОшибки();
		Отказ = Истина;	
	КонецПопытки;
	
	Возврат КнигаЭксель;	
КонецФункции

Функция ДобавитьЛистExcel(КнигаЭксель, ИмяЛиста = "", Отказ, ОписаниеОшибки) Экспорт
	
	ЛистЭксель = Неопределено;
	Попытка
		ЛистЭксель = КнигаЭксель.Worksheets.Add();
		
		Если ЗначениеЗаполнено(ИмяЛиста) Тогда
			ЛистЭксель.Name = ИмяЛиста;
		КонецЕсли;
	Исключение
		ОписаниеОшибки = "Не удалось добавить лист. Описание ошибки: " + ОписаниеОшибки();
		Отказ = Истина;	
	КонецПопытки;
	
	Возврат ЛистЭксель;
КонецФункции

////ЗАКРЫТЬ////
Процедура ЗакрытьКнигуExcel(КнигаЭксель, Сохранение = 0) Экспорт
	
	Попытка
		КнигаЭксель.Close(Сохранение);
	Исключение
	КонецПопытки;
		
	КнигаЭксель = Неопределено;
	
КонецПроцедуры

Процедура ЗакрытьExcel(Эксель) Экспорт
	
	Попытка
		Эксель.DisplayAlerts = 0;
		Эксель.Quit();
	Исключение
	КонецПопытки;
	
	Эксель = Неопределено;
	
КонецПроцедуры

#КонецОбласти

#Область Прочитать

//реализация через ComSafeArray
Функция ПрочитатьЛистExcel(ЛистЭксель, НомерПервойСтроки = 1, НомерПервойКолонки = 1, ВсегоСтрок = 0, ВсегоКолонок = 0) Экспорт
		
	Если ВсегоСтрок = 0 Тогда
		ВсегоСтрок = ЛистЭксель.Cells.SpecialCells(11).Row;
	КонецЕсли;
	Если ВсегоКолонок = 0 Тогда
		ВсегоКолонок = ЛистЭксель.Cells.SpecialCells(11).Column;
	КонецЕсли;
	
	ОписаниеТиповСтрока = Новый ОписаниеТипов("Строка");
	
	ТЗ =  Новый ТаблицаЗначений;
	Для Счетчик = 1 По ВсегоКолонок Цикл
		СчетчикСтрокой = ОписаниеТиповСтрока.ПривестиЗначение(Счетчик);
		СчетчикСтрокой = СтрЗаменить(СчетчикСтрокой, Символы.НПП, "");
		
		ТЗ.Колонки.Добавить("Колонка"+СчетчикСтрокой, ОписаниеТиповСтрока);
	КонецЦикла;
	
	Для Счетчик = НомерПервойСтроки По ВсегоСтрок Цикл
		НоваяСтрока = ТЗ.Добавить();
	КонецЦикла;
	
	Область = ЛистЭксель.Range(ЛистЭксель.Cells(НомерПервойСтроки,НомерПервойКолонки), ЛистЭксель.Cells(ВсегоСтрок,ВсегоКолонок));
	Данные = Область.Value.Выгрузить();
	
	Для Счетчик = 0 По ВсегоКолонок-1 Цикл
		ТЗ.ЗагрузитьКолонку(Данные[Счетчик], Счетчик);
	КонецЦикла;
		
	Возврат ТЗ;
КонецФункции

#КонецОбласти

#Область Записать

//реализация через ComSafeArray 
Функция ЗаполнитьЛистExcelИзТаблицыЗначений(ЛистExcel, ТаблицаЗначений) Экспорт
	
	ВсегоКолонок = ТаблицаЗначений.Колонки.Количество();
	ВсегоСтрок = ТаблицаЗначений.Количество();
	
	// Создание COMSafeArray
	МассивКом = Новый COMSafeArray("VT_Variant", ВсегоКолонок, ВсегоСтрок);
	
	// Заполнение COMSafeArray
	Для Стр = 0 По ВсегоСтрок - 1 Цикл
		Для Кол = 0 По ВсегоКолонок - 1 Цикл
			МассивКом.SetValue(Кол, Стр, ТаблицаЗначений[Стр][Кол]);
		КонецЦикла;
	КонецЦикла;
	// Присвоение области листа Excel значений из COMSafeArray
	ЛистExcel.Range(ЛистExcel.Cells(1, 1), ЛистExcel.Cells(ВсегоСтрок, ВсегоКолонок)).Value = МассивКом;
	
КонецФункции

#КонецОбласти

#Область Служебные

////ПРИМЕР ИСПОЛЬЗОВАНИЯ////
Процедура Пример(ПутьКФайлу)
	
	Отказ = Ложь;
	ОписаниеОшибки = "";
	
	Эксель = ПолучитьExcel(Отказ, ОписаниеОшибки);
	Если Отказ Тогда
		Сообщить(ОписаниеОшибки);
		Возврат;
	КонецЕсли;
	
	КнигаЭксель = ПолучитьКнигуExcel(Эксель, ПутьКФайлу, Отказ, ОписаниеОшибки);
	Если Отказ Тогда
		Сообщить(ОписаниеОшибки);
		Возврат;	
	КонецЕсли;
	
	КоличествоЛистов = КнигаЭксель.Sheets.Count;
	Для СчетчикПоЛистам = 1 По КоличествоЛистов Цикл
		Отказ = Ложь;
		ОписаниеОшибки = "";
		ЛистЭксель = ПолучитьЛистExcel(КнигаЭксель, СчетчикПоЛистам, Отказ, ОписаниеОшибки);
		Если Отказ Тогда
			Сообщить("Не удалось открыть лист номер " + СчетчикПоЛистам + ". ОписаниеОшибки: " + ОписаниеОшибки);
			Продолжить;
		КонецЕсли;
		ТЧ = ПрочитатьЛистExcel(ЛистЭксель);
		
	КонецЦикла;
	
	ЗакрытьКнигуExcel(КнигаЭксель);
	ЗакрытьExcel(Эксель);
	
КонецПроцедуры

////ЗАКРЫТИЕ ПРОЦЕССОВ////
Процедура УбитьЗависшиеПроцессы() Экспорт
	//убивает процессы запущенные под текущим пользователем
	
	ИскатьЗависшиеПроцессы = Истина;
	ВыходИзРекурсии = 50; //50 попыток, потом выходим
	Пока ИскатьЗависшиеПроцессы Цикл
		
		Если ВыходИзРекурсии = 0 Тогда
			ИскатьЗависшиеПроцессы = Ложь;	
		КонецЕсли;
		
		Попытка
			Эксель = ПолучитьCOMОбъект(, "Excel.Application");
			ЗакрытьExcel(Эксель);
			ВыходИзРекурсии = ВыходИзРекурсии -1;
		Исключение
			ИскатьЗависшиеПроцессы = Ложь;
		КонецПопытки;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти
